buildscript {
    repositories {
        maven {
            name 'Forge'
            url 'https://maven.minecraftforge.net'
        }
        maven {
            name 'Sponge'
            url 'https://repo.spongepowered.org/maven'
        }
        maven {
            name 'Parchment'
            url 'https://maven.parchmentmc.org'
        }
        maven {
            name 'Gradle'
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7.+', changing: true
        classpath group: 'org.parchmentmc', name: 'librarian', version: '1.+', changing: true
        classpath group: 'io.github.0ffz', name: 'gpr-for-gradle', version: '1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'io.github.0ffz.github-packages'

version = project.mc_version + '-' + project.aether_version
group = 'com.gildedgames.aether' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'aether'

java.toolchain.languageVersion = JavaLanguageVersion.of(17) // Mojang ships Java 8 to end users, so your mod should target Java 8.

minecraft {
    mappings channel: 'parchment', version: project.mappings

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            //Only uncomment this if you actually need the debug logging! If you enable this the log will be full of incredibly useless information spam
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            //property 'forge.logging.console.level', 'debug'

            mods {
                aether {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            //property 'forge.logging.console.level', 'debug'

            mods {
                aether {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            //property 'forge.logging.console.level', 'debug'

            args '--mod', 'aether', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing', file('src/generated/resources/')

            environment 'target', 'fmluserdevdata'

            mods {
                aether {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.mc_version}-${project.forge_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

//    compileOnly "com.gildedgames.nitrogen:nitrogen:${project.nitrogen_version}"
//    runtimeOnly fg.deobf("com.gildedgames.nitrogen:nitrogen:${project.nitrogen_version}")

    compileOnly "top.theillusivec4.curios:curios-forge:${project.curios_version}:api"
    compileOnly "top.theillusivec4.curios:curios-forge:${project.curios_version}"
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${project.curios_version}")

    compileOnly "top.theillusivec4.caelus:caelus-forge:${project.caelus_version}:api"
    compileOnly "top.theillusivec4.caelus:caelus-forge:${project.caelus_version}"

//    jarJar fg.deobf("com.gildedgames.nitrogen:nitrogen:${project.nitrogen_version}") {
//        jarJar.ranged(it, "[${project.nitrogen_version},)")
//        jarJar.pin(it, "${project.nitrogen_version}")
//    }

    jarJar fg.deobf("top.theillusivec4.curios:curios-forge:${project.curios_version}") {
        jarJar.ranged(it, "[${project.curios_version},)")
        jarJar.pin(it, "${project.curios_version}")
    }

    // compileOnly fg.deobf("com.blamejared.crafttweaker:CraftTweaker-${project.crafttweaker_version}")

    //compileOnly fg.deobf("curse.maven:colytra-280200:${project.colytra_version}")
    //compileOnly fg.deobf("curse.maven:curious-elytra-317716:${project.curiouselytra_version}")

    //runtimeOnly fg.deobf("io.github.ultimateboomer:smoothboot-forge:${project.smoothboot_version}")

    //compileOnly fileTree(dir: 'libs', include: '*.jar')

    compileOnly(fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}"))
    compileOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}"))
    runtimeOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}"))
}

repositories {
    maven { url = "https://maven.blamejared.com" }
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url = "https://www.cursemaven.com" }
    maven { url = "https://dvs1.progwml6.com/files/maven/"}
//    maven githubPackage.invoke("Gilded-Games/Nitrogen")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : "Gilded Games",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Gilded Games",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "aether.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

reobf.create('jarJar')

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}

tasks.jarJar.configure {
    archiveClassifier = ""
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

mixin {
    config "aether.mixins.json"
    add sourceSets.main, "aether.refmap.json"
}

build {
    dependsOn 'jarJar'
}
