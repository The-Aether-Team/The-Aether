buildscript {
    repositories {
        maven {
            name 'Forge'
            url 'https://maven.minecraftforge.net'
        }
        maven {
            name 'Sponge'
            url 'https://repo.spongepowered.org/maven'
        }
        maven {
            name 'Parchment'
            url 'https://maven.parchmentmc.org'
        }
        maven {
            name 'Gradle'
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7.+', changing: true
        classpath group: 'org.parchmentmc', name: 'librarian', version: '1.+', changing: true
        classpath group: 'io.github.0ffz', name: 'gpr-for-gradle', version: '1.+', changing: true
        classpath group: 'gradle.plugin.com.matthewprenger', name: 'CurseGradle', version: '1.4.+', changing: true
        classpath group: 'com.modrinth.minotaur', name: 'Minotaur', version: '2.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'io.github.0ffz.github-packages'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'com.modrinth.minotaur'

version = project.mc_version + '-' + project.aether_version + '-forge'
group = 'com.gildedgames.aether' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'aether'

java.toolchain.languageVersion = JavaLanguageVersion.of(17) // Mojang ships Java 8 to end users, so your mod should target Java 8.

minecraft {
    mappings channel: 'parchment', version: project.mappings

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            //Only uncomment this if you actually need the debug logging! If you enable this the log will be full of incredibly useless information spam
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            //property 'forge.logging.console.level', 'debug'

            mods {
                aether {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            //property 'forge.logging.console.level', 'debug'

            mods {
                aether {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            //property 'forge.logging.console.level', 'debug'

            args '--mod', 'aether', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing', file('src/generated/resources/')

            environment 'target', 'fmluserdevdata'

            mods {
                aether {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.mc_version}-${project.forge_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

//    compileOnly "com.gildedgames.nitrogen:nitrogen:${project.nitrogen_version}"
//    runtimeOnly fg.deobf("com.gildedgames.nitrogen:nitrogen:${project.nitrogen_version}")

    implementation fg.deobf("top.theillusivec4.curios:curios-forge:${project.curios_version}")

    compileOnly fg.deobf("top.theillusivec4.caelus:caelus-forge:${project.caelus_version}")

    compileOnly "mezz.jei:jei-${project.mc_version}-common-api:${project.jei_version}"
    compileOnly "mezz.jei:jei-${project.mc_version}-forge-api:${project.jei_version}"
    runtimeOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}"))

//    runtimeOnly fg.deobf("io.github.flemmli97:debugutils:${project.mc_version}-${project.debugutils_version}-forge")

//    jarJar fg.deobf("com.gildedgames.nitrogen:nitrogen:${project.nitrogen_version}") {
//        jarJar.ranged(it, "[${project.nitrogen_version},)")
//        jarJar.pin(it, "${project.nitrogen_version}")
//    }

    jarJar fg.deobf("top.theillusivec4.curios:curios-forge:${project.curios_version}") {
        jarJar.ranged(it, "[${project.curios_version},)")
        jarJar.pin(it, "${project.curios_version}")
    }

    // compileOnly fg.deobf("com.blamejared.crafttweaker:CraftTweaker-${project.crafttweaker_version}")

    //compileOnly fg.deobf("curse.maven:colytra-280200:${project.colytra_version}")
    //compileOnly fg.deobf("curse.maven:curious-elytra-317716:${project.curiouselytra_version}")

    //runtimeOnly fg.deobf("io.github.ultimateboomer:smoothboot-forge:${project.smoothboot_version}")

    //compileOnly fileTree(dir: 'libs', include: '*.jar')
}

repositories {
    maven { url = "https://maven.blamejared.com" }
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url = "https://www.cursemaven.com" }
    maven { url = "https://dvs1.progwml6.com/files/maven/" }
    maven { url = "https://gitlab.com/api/v4/projects/21830712/packages/maven" }
//    maven githubPackage.invoke("Gilded-Games/Nitrogen")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "The Aether",
                "Specification-Vendor"    : "Gilded Games",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : "The Aether",
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Gilded Games",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "aether.mixins.json"
        ])
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

artifacts {
    archives sourcesJar
}

jar.finalizedBy('reobfJar')

reobf.create('jarJar')

def reobfFile = file("$buildDir/reobfJar/output.jar")
def jar = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}

tasks.jarJar.configure {
    archiveClassifier = ""
}

tasks.named(sourceSets.main.compileJavaTaskName) {
    outputs.file("${it.temporaryDir}/${it.name}-refmap.json").withPropertyName("mixin refmap")
}

compileJava {
    options.incremental = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            artifact jar
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Gilded-Games/The-Aether"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

curseforge {
    project {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        id = "${project.curseforge_id}"
        releaseType = "${project.version_type}"
        changelog = file("$rootDir/docs/CHANGELOG.md")
        changelogType = "markdown"
        addGameVersion("${project.mc_version}")
        mainArtifact(tasks.jar) {
            displayName = "The Aether - Forge - ${version}"
            relations {
                embeddedLibrary "curios"
                optionalDependency "starlight-forge"
            }
        }
    }
    options {
        debug = true
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${project.modrinth_id}"
    versionNumber = "${version}"
    versionType = "${project.version_type}"
    versionName = "The Aether - Forge - ${version}"
    changelog = new File("$rootDir/docs/CHANGELOG.md").text
    uploadFile = tasks.jar
    gameVersions = ["${project.mc_version}"]
    loaders = ["forge"]
    dependencies {
        embedded.version "curios", "${project.curios_version}"
        optional.project "starlight-forge"
    }
    debugMode = true
}

mixin {
    config "aether.mixins.json"
    add sourceSets.main, "aether.refmap.json"
}

build {
    dependsOn 'jarJar'
}
